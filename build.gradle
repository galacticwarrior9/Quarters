plugins {
    id "java"
    id "maven-publish"
}

def semVersion = "1.0.7"

def static gitBranch() {
    "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

def static gitVersion() {
    "git rev-parse --short HEAD".execute().text.trim()
}

group = "au.lupine"
version = "${semVersion}+${gitBranch()}+${gitVersion()}"

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }

    maven {
        url = "https://repo.glaremasters.me/repository/towny/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
    compileOnly("com.palmergames.bukkit.towny:towny:0.101.0.0")
    compileOnly("me.clip:placeholderapi:2.11.6")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.named("jar") {
    archiveFileName = "${project.name}-${version}.jar"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }

    from("$projectDir") {
        include("LICENSE")
        into("")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}